apply plugin: 'com.android.application'
apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.projectName", "Port Authority"
        property "sonar.projectKey", "portauthority"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "."
        property "sonar.exclusions", "build/**,**/*.png,**/*.psd"
        property "sonar.import_unknown_files", true
        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 47
        versionName "1.9.9"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def formattedDate = new Date().format('yyyyMMdd')
                    def newName = output.outputFile.name
                    newName = newName.replace("app", "PortAuthority")
                    newName = newName.replace("-release", "-release-" + formattedDate)
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
    }

    productFlavors {
        free {
            applicationId "com.aaronjwood.portauthority.free"
        }
        donate {
            applicationId "com.aaronjwood.portauthority.donate"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'jcifs:jcifs:1.3.17'
    compile 'dnsjava:dnsjava:2.1.7'
    //This does absolutely nothing
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
