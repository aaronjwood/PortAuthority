apply plugin: 'com.android.application'
apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.projectName", "Port Authority"
        property "sonar.projectKey", "portauthority"
        property "sonar.projectVersion", "1.0"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "."
        property "sonar.exclusions", "build/**,**/*.png,**/*.psd"
        property "sonar.import_unknown_files", true
        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
    }
}

android {
    viewBinding.enabled = true
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 59
        versionName "2.3.1"
        applicationId "com.aaronjwood.portauthority"
        setProperty("archivesBaseName", "PortAuthority-$versionName")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    flavorDimensions "main"

    productFlavors {
        free {
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            dimension "main"
        }
        donate {
            applicationIdSuffix ".donate"
            versionNameSuffix "-donate"
            dimension "main"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.9' // Anything past 3.12.x will break our Android 4 support!
    implementation 'jcifs:jcifs:1.3.17'
    implementation 'org.minidns:minidns-hla:0.3.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}
